/*
MIT License
Copyright (c) 2021 Dipjyoti Metia
Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:
The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.
THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
 */

import org.apache.avro.Conversions

plugins {
    id "java"
    id "idea"
    id "org.openapi.generator" version "6.5.0"
    id "com.github.davidmc24.gradle.plugin.avro" version "1.7.0"
    id 'com.google.protobuf' version '0.9.3'
}

ext {
    grpcVersion = '1.54.1'
    protoVersion = '3.22.3'
}

group 'com.HybridTestFramework'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

def apiSpec = "zephyr" //xray/zephyr/qtest
def specName = "api.cloud.expanded.yml"
openApiGenerate {
    generatorName = "java"
    inputSpec = "$projectDir/src/main/openapi/${apiSpec}/${specName}".toString()
    outputDir = "$projectDir/generated/${apiSpec}".toString()
    apiPackage = "org.openapi.${apiSpec}.api"
    invokerPackage = "org.openapi.${apiSpec}.invoker"
    modelPackage = "org.openapi.${apiSpec}.model"
    configOptions = [
            dateLibrary: "java8"
    ]
    globalProperties = [
            modelDocs: "false"
    ]
}

//sourceSets.main.java.srcDirs = [
//        "${projectDir}/generated/xray/src/main/java",
//        "${projectDir}/generated/zephyr/src/main/java",
//        "${projectDir}/generated/qtest/src/main/java"
//]

openApiValidate {
    inputSpec = ""
    recommend = true
}

avro {
    stringType = "CharSequence"
    fieldVisibility = "private"
    customConversion Conversions.UUIDConversion
}

sourceSets {
    main {
        proto {
            srcDir 'src/main/proto'
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protoVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
//    generatedFilesBaseDir = "$projectDir/src/main/gen"
}

//clean {
//    delete protobuf.generatedFilesBaseDir
//}

tasks.withType(Copy).configureEach { duplicatesStrategy 'exclude' }

dependencies {
    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "io.grpc:grpc-all:${grpcVersion}"
    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
    implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+
    implementation "com.google.protobuf:protobuf-java:${protoVersion}"
    runtimeOnly "com.google.protobuf:protobuf-java-util:${protoVersion}"
    implementation group: 'org.apache.avro', name: 'avro', version: '1.11.1'
    implementation 'org.openapitools:openapi-generator:6.5.0'
}